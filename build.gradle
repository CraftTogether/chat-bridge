plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

group = 'com.github.craftToegether'
version = '1.3-SNAPSHOT-1'

repositories {
    mavenCentral()
    maven {
        name = 'papermc-repo'
        url = 'https://papermc.io/repo/repository/maven-public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        url = "https://jitpack.io"
    }
}

shadowJar {
    archiveFileName = "${baseName}-${version}.jar";
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.18.1-R0.1-SNAPSHOT'
    implementation group: 'com.squareup.okhttp', name: 'okhttp', version: '2.7.5' // OkHttp
    implementation group: 'org.json', name: 'json', version: '20201115' // JSON
    implementation "com.gitlab.PolarianDev:ircj:1.1-SNAPSHOT-6"
    compileOnly "com.github.CraftTogether:Kelp:1.2-SNAPSHOT-1"
    compileOnly "net.dv8tion:JDA:5.0.0-alpha.4"
    compileOnly "com.github.CraftTogether:Rinku:1.0-SNAPSHOT-5"
    compileOnly "com.github.CraftTogether:weg:1.0"
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
